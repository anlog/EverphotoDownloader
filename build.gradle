buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        google()
        jcenter()

    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

apply plugin: 'application'

mainClassName = 'cc.ifnot.everphotodownloader.EverphotoDownloader'

ext {
    def cmd = 'git rev-list HEAD --count --first-parent'
    version_code = cmd.execute().text.trim().toInteger()
    version_suffix = 'git rev-parse --short HEAD'.execute().text.trim()
    ep_version_name = (version_code / 100).toInteger() + ".${version_code}-${version_suffix}"
    ep_debug_level = 0
}

sourceSets {
    main.java.srcDirs += "$buildDir/generated/sources/config/java/main"
}

compileJava {
    dependsOn 'copyJavaConfig'
}

task copyJavaConfig(type: Copy) {
    from 'src/main/config'
    into "$buildDir/generated/sources/config/java/main"
    expand('name': project.name,
            'ep_version': ep_version_name,
            'ep_debug': ep_debug_level)
    filteringCharset = 'UTF-8'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':utils')

    implementation 'commons-cli:commons-cli:1.4'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8